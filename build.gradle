buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.1'
        classpath 'com.ryanharter.gradle-git-repo:gradle-plugin:1.0.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'git-repo'


android {
    compileSdkVersion 19
    buildToolsVersion '23.0.0'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        versionCode 1
        versionName '0.0.1'
        minSdkVersion 19
        targetSdkVersion 19

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testHandleProfiling true
        testFunctionalTest true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'git@github.com:decamp/adraw.git/releases')
            snapshotRepository(url: 'git@github.com:decamp/adraw.git/snapshots')

            pom {
                groupId = 'com.bitstillery'
                artifactId = 'adraw'
                version = android.defaultConfig.versionName

                project {
                    licenses {
                        license {
                            name 'BSD 2-Clause License'
                            url 'http://opensource.org/licenses/BSD-2-Clause'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'decamp'
                            name 'Philip DeCamp'
                        }
                    }
                }
            }
        }
    }
}
